100 'SETRTC.BAS
110 'BEFORE RUNNING THIS PROGRAM USE THE COMMAND
120 'SET DATE year, month, date, hour, minute, second
130 'EXAMPLE: SET DATE 2021, 2, 16, 9, 45, 0
140 'THEN RUN THIS PROGRAM TO UPDATE THE RTC WITH
150 'THE CURRENT SYSTEM TIME OF THE BASIC ENGINE
160 RTCADR=104'I2C ADDRESS 0x68
170 GET TIME HRS,MIN,SEC
180 GET DATE YRS,MTH,DAT,DAY
190 YRS=YRS-2000'THE RTC ONLY STORES 0-99
200 OUT$=CHR$(0)'REGISTER 0
210 SEC$=CHR$(FN DEC2BCD(SEC)):OUT$=OUT$+SEC$
220 MIN$=CHR$(FN DEC2BCD(MIN)):OUT$=OUT$+MIN$
230 HRS$=CHR$(FN DEC2BCD(HRS)):OUT$=OUT$+HRS$
240 DAY$=CHR$(FN DEC2BCD(DAY)):OUT$=OUT$+DAY$
250 DAT$=CHR$(FN DEC2BCD(DAT)):OUT$=OUT$+DAT$
260 MTH$=CHR$(FN DEC2BCD(MTH)):OUT$=OUT$+MTH$
270 YRS$=CHR$(FN DEC2BCD(YRS)):OUT$=OUT$+YRS$
280 A=I2CW(RTCADR,OUT$)
290 PRINT "RTC NOW SET TO CURRENT SYSTEM TIME"
300 END 
310 PROC DEC2BCD(DEC)
320 RETURN (INT(@DEC/10)*16)+INT((@DEC MOD 10))
330 PROC BCD2DEC(BCD)
340 RETURN (INT(@BCD/16)*10)+INT((@BCD MOD 16))
